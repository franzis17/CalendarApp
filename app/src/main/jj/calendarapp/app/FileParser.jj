PARSER_BEGIN(FileParser)

package calendarapp.app;

import calendarapp.app.utils.*;
import calendarapp.app.Event.*;

import java.io.*;
import java.time.*;

/**
 * Read a file and parse the file
 */
public class FileParser
{
    private static CalendarHandler calendarHandler;
    
    /** Used for adding plugin classpaths to the pluginHandler(loader of plugins) */
    private static PluginHandler pluginHandler;
    private static ScriptHandler scriptHandler;
    
    public void setCalendarHandler(CalendarHandler calendarHandler)
    {
        this.calendarHandler = calendarHandler;
    }
    
    public void setPluginHandler(PluginHandler pluginHandler)
    {
        this.pluginHandler = pluginHandler;
    }
    
    public void setScriptHandler(ScriptHandler scriptHandler)
    {
        this.scriptHandler = scriptHandler;
    }
}

PARSER_END(FileParser)

SKIP: { " " | "\n" | "\t" | "\r" | "\r\n" | "," }

/*
  [ What are Tokens? ]
  
  > Tokens are the individual "words" (unit of information) contained in a file,
    which needs to be "parsed"/"read".
*/

// General Tokens
TOKEN : {
    < COLON: ":" >
|   < NUM: ["0"-"9"] >
}

// Specific Tokens
TOKEN : {
// Type Identifiers
    < EVENT: "event" >
|   < PLUGIN: "plugin" >
|   < SCRIPT: "script" >

// Event Tokens
|   < DATE: <NUM> (<NUM>)* "-" <NUM> (<NUM>)* "-" <NUM> (<NUM>)* >
|   < TIME: <NUM> (<NUM>)* ":" <NUM> (<NUM>)* ":" <NUM> (<NUM>)* >
|   < DURATION: (<NUM>)+ >
|   < STRING: "\"" (~["\""])* "\"" >
|   < ALL_DAY: "all-day" >

// Plugin Tokens
|   < KEY: (["a"-"z", "A"-"Z", "0"-"9", "_"])+ >
|   < OPEN_BRACE: "{" >
|   < CLOSE_BRACE: "}" >
|   < IDENTIFIER: (["a"-"z", "A"-"Z", "_", "$"]) (["a"-"z", "A"-"Z", "_", "$", "0"-"9"])* >
|   < PLUGIN_PACKAGE_NAME: <IDENTIFIER> ( "." <IDENTIFIER>)* >

// Script Tokens
|   < SCRIPT_CODE: "\"" ( (~["\""]) | ("\"\"") )* "\"" >
}

/**
 * Root of BNF production
 */
void parseFile() :
{
}
{
    // File should contain details of either multiples of "events", "plugins", or "scripts"
    ( ParseEvent() | ParsePlugin() | ParseScript() )+
    <EOF>
}

/**
 * Parse an event if it contains the following patterns of event details
 */
void ParseEvent() :
{
}
{
    {
        System.out.println("\n> Parsing event...");
    }
    <EVENT>
    ( AllDayEvent() | HourlyEvent() )
}

void AllDayEvent() :
{
    Token dateToken, titleToken;
    String dateStr, title;
}
{
    dateToken=<DATE> <ALL_DAY> titleToken=<STRING>
    {
        dateStr = dateToken.image;
        title = StringUtility.removeFirstAndLastChar(titleToken.image);
        System.out.println("All-Day Event:\n" +
            "   date = " + dateStr + "\n" +
            "  title = " + title
        );
        
        // add all day event
        LocalDate date = StringUtility.stringToDate(dateStr);
        calendarHandler.addEvent(new AllDayEvent(date, title));
    }
}

void HourlyEvent() :
{
    Token dateToken, timeToken, titleToken, durationToken;
    String dateStr, timeStr, title;
    int duration;
}
{
    dateToken=<DATE> timeToken=<TIME> durationToken=<DURATION> titleToken=<STRING>
    {
        dateStr = dateToken.image;
        timeStr = timeToken.image;
        duration = Integer.parseInt(durationToken.image);
        title = StringUtility.removeFirstAndLastChar(titleToken.image);
        System.out.println("Hourly Event:\n" + 
            "      date = " + dateStr + "\n" +
            "      time = " + timeStr + "\n" +
            "  duration = " + duration + "\n" +
            "     title = " + title
        );
        
        // add hourly event
        LocalDate date = StringUtility.stringToDate(dateStr);
        LocalTime time = StringUtility.stringToTime(timeStr);
        calendarHandler.addEvent(new HourlyEvent(date, title, time, duration));
    }
}

/**
 * Parse a Plugin if it contains the following patterns of plugin details
 */
void ParsePlugin() :
{
    Token pluginIDToken, keyToken, valueToken;
    String pluginID, key, value;
}
{
    {
        System.out.println("\n> Parsing plugin...");
    }
    
    <PLUGIN> pluginIDToken=<PLUGIN_PACKAGE_NAME>
    {
        pluginID = pluginIDToken.image;
        System.out.println("Plugin id = " + pluginID);
        // Add the plugin classpath (pluginID) to the list of plugins in main app client
        pluginHandler.addPluginByClasspath(pluginID);
    }
    <OPEN_BRACE>
        // Contains zero or more key-value pairs
        (
            keyToken=<KEY> <COLON> valueToken=<STRING>
            {
                key = keyToken.image;
                value = StringUtility.removeFirstAndLastChar(valueToken.image);
                System.out.println("+ Key-value pair found:");
                System.out.println("Key = " + key + ", value = " + value);
            }
        )*
    <CLOSE_BRACE>
}

/**
 * Parse a Script if it contains the word "script"
 */
void ParseScript() :
{
    Token scriptCodeToken;
    String scriptCodeStr;
}
{
    {
        System.out.println("\n> Parsing Script...");
    }
    <SCRIPT>
    scriptCodeToken=<SCRIPT_CODE>
    {
        scriptCodeStr = StringUtility.removeFirstAndLastChar(scriptCodeToken.image);
        System.out.println("Script Code:\n" + scriptCodeStr);
        
        scriptHandler.runScript(scriptCodeStr);
    }
}
